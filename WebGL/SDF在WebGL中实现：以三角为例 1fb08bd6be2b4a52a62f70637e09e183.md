# SDF在WebGL中的实现：以三角为例

本文以三角为例，在WebGL中实现SDF计算和实时渲染。

[SDF in WebGL](https://observablehq.com/@listenzcc/sdf-in-webgl)

---
[toc]

## 三角形的SDF实时渲染

本文对三角形的Signed Distance Function（SDF）进行实时渲染，采用的技术手段是WebGL。SDF是一种描述几何形状的方法，WebGL是一种用于在Web浏览器中进行3D图形渲染的JavaScript API。通过WebGL，开发者可以直接利用GPU进行高性能的图形计算，从而实现实时渲染。在这种情况下，你可能使用WebGL来执行SDF的计算，并将结果传递给着色器程序进行渲染。下图中，SDF数值用红色（`+`）或黑色（`=`）表示，；等值线用白色（`+`）或黑色（`=`）的封闭曲线表示。

![20240114-185944.gif](SDF%E5%9C%A8WebGL%E4%B8%AD%E5%AE%9E%E7%8E%B0%EF%BC%9A%E4%BB%A5%E4%B8%89%E8%A7%92%E4%B8%BA%E4%BE%8B%201fb08bd6be2b4a52a62f70637e09e183/20240114-185944.gif)

![Untitled](SDF%E5%9C%A8WebGL%E4%B8%AD%E5%AE%9E%E7%8E%B0%EF%BC%9A%E4%BB%A5%E4%B8%89%E8%A7%92%E4%B8%BA%E4%BE%8B%201fb08bd6be2b4a52a62f70637e09e183/Untitled.png)

![Untitled](SDF%E5%9C%A8WebGL%E4%B8%AD%E5%AE%9E%E7%8E%B0%EF%BC%9A%E4%BB%A5%E4%B8%89%E8%A7%92%E4%B8%BA%E4%BE%8B%201fb08bd6be2b4a52a62f70637e09e183/Untitled%201.png)

![Untitled](SDF%E5%9C%A8WebGL%E4%B8%AD%E5%AE%9E%E7%8E%B0%EF%BC%9A%E4%BB%A5%E4%B8%89%E8%A7%92%E4%B8%BA%E4%BE%8B%201fb08bd6be2b4a52a62f70637e09e183/Untitled%202.png)

本文使用的`sdf` 函数的简要介绍如下**：**

- 输入参数：三个二维向量 `p`、`v` 和 `c`，`c` 是三角形的中心点。
- 定义了法向量 `u`，它是三角形边向量 `v` 的垂直向量（通过将 `x` 和 `y` 互换得到）`u`，$u \cdot v = 0$。
- 返回值：计算并返回给定点 `p` 关于三角形边缘的SDF。
    - 如果点 `p` 和中心点 `c` 在法向量 `u` 上的投影方向一致（点积的符号相同），则说明点 `p` 在三角形内部，此时返回负的SDF值，即 `abs(dot(p, u) / sqrt(dot(u, u)))`。
    - 如果点 `p` 不在三角形内部，根据点 `p` 在三角形边上的位置，分别计算三种情况下的距离：
        - 如果点 `p` 在边的起始点之前（`pv < 0.0`），返回点 `p` 到起始点的距离；
        - 如果点 `p` 在边的结束点之后（`pv > vv`），返回点 `p` 到结束点的距离；
        - 如果点 `p` 在边的中间，返回点 `p` 到边的距离，即 `abs(dot(p, u) / sqrt(dot(u, u)))`。
    - 最终，得到的距离值代表了点 `p` 到三角形边缘的距离，正负值表示点在三角形的内部还是外部。

这样的计算方式符合SDF的定义，用于在渲染中判断点在三角形内部还是外部，并计算具体的SDF值。

## 附录：计算库myRegl

```cpp
float dist(vec2 a, vec2 b) {
  return sqrt(dot(a-b, a-b));
}

/**
 * Compute sdf of p, v, c
 * c: The center of the triangle;
 * p: The given point whose sdf is computed;
 * v: The end point of the triangle's endge.
 **/
float sdf(vec2 p, vec2 v, vec2 c) {
  vec2 u = vec2(v.y, -v.x);
  if (dot(c, u) * dot(p, u) >= 0.0) {
    return -abs(dot(p, u) / sqrt(dot(u, u)));
  }

  float vv = dot(v, v);
  float pv = dot(p, v);
  if (pv < 0.0) {
    return sqrt(dot(p, p));
  }
  if (pv > vv) {
    return sqrt(dot(p-v, p-v));
  }
  return abs(dot(p, u) / sqrt(dot(u, u)));
}

float wireframe (float parameter, float width, float feather) {
  float w1 = width - feather * 0.5;
  float d = fwidth(parameter);
  float looped = 0.5 - abs(mod(parameter, 0.1) - 0.5);
  return smoothstep(d * w1, d * (w1 + feather), looped);
}
```

## 附录：实例代码

```cpp
#extension GL_OES_standard_derivatives : enable
precision mediump float;
${myRegl}
uniform mat4 projection, view, triangle;
varying vec2 vposition;
void main () {
  /**
   * The a is not quite a good name,
   * it refers the projected position of triangle points.
   * The triangle is a mat4 of
   * [x1, y1, z1, 0, x2, y2, z2, 0, x3, y3, z3, 0, 0, 0, 0, 1]
   **/
  mat4 a = projection * view * triangle;
  float d = 0.0;
  d = dist(vposition, vec2(a[0]));

  /**
   * Compute the sdf of three triangle edges,
   * if the sdfs are all negative, the point is inside the triangle, select it largest value;
   * if there are positive sdfs, the point is outside the triangle, select is smallest value from the positive ones.
   **/ 
  vec4 center = (a[0] + a[1] + a[2]) / 3.0;
  float d1 = sdf(vposition-vec2(a[0]), vec2(a[1]-a[0]), vec2(center-a[0]));
  float d2 = sdf(vposition-vec2(a[1]), vec2(a[2]-a[1]), vec2(center-a[1]));
  float d3 = sdf(vposition-vec2(a[2]), vec2(a[0]-a[2]), vec2(center-a[2]));

  d = max(max(d1, d2), d3);
  if (d > 0.0) {
    if (d > d1 && d1 > 0.0) {d = d1;}
    if (d > d2 && d2 > 0.0) {d = d2;}
    if (d > d3 && d3 > 0.0) {d = d3;}
  }

  gl_FragColor = mix(vec4(vec3(d), 1.0), vec4(0.5 + d, 0.5, 0.5, 1.0), wireframe(d, 2.0, 0.1));
}
```